# شروع شدن

Starknet یک راه حل لایه 2 مقیاس پذیر در اتریوم است. این راهنما شما را در فرآیند استقرار و تعامل با اولین قرارداد هوشمند Starknet با استفاده از زبان برنامه‌نویسی Cairo راهنمایی می‌کند، زبانی که برای ایجاد اثبات اعتبار طراحی شده و استارک‌نت از آن استفاده می‌کند. برای توسعه دهندگان باتجربه ای که به دنبال درک مفاهیم اصلی و کسب تجربه عملی هستند، این راهنما دستورالعمل های گام به گام و جزئیات ضروری را ارائه می دهد.

ما از پلاگین Starknet Remix برای کامپایل، استقرار و تعامل با قرارداد هوشمند خود استفاده خواهیم کرد. این یک ابزار عالی برای شروع توسعه Starknet است.

1. از پروژه ریمیکس دیدن کنید.
2. به بخش «افزونه‌ها» در گوشه پایین سمت چپ بروید.
3. افزونه Starknet را فعال کنید.

![Activate the Starknet Plugin](img/ch01-starknet-plugin.png)

پلاگین Starknet را فعال کنید

4. پس از فعال کردن، لوگوی Starknet در نوار کناری سمت چپ ظاهر می شود. برای تعامل با فایل‌های باز شده قاهره روی آن کلیک کنید.

### مقدمه ای بر قراردادهای هوشمند Starknet

اسکریپت زیر یک الگوی قرارداد Ownable ساده است که در قاهره برای Starknet نوشته شده است. ویژگی های آن:

* یک سیستم مالکیت
* روشی برای انتقال مالکیت
* روشی برای بررسی مالک فعلی
* یک اعلان رویداد برای تغییرات مالکیت.

```rust
use starknet::ContractAddress;

#[starknet::interface]
trait OwnableTrait<T> {
    fn transfer_ownership(ref self: T, new_owner: ContractAddress);
    fn get_owner(self: @T) -> ContractAddress;
}

#[starknet::contract]
mod Ownable {
    use super::ContractAddress;
    use starknet::get_caller_address;

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
      OwnershipTransferred1: OwnershipTransferred1,
    }

    #[derive(Drop, starknet::Event)]
    struct OwnershipTransferred1 {
        #[key]
        prev_owner: ContractAddress,
        #[key]
        new_owner: ContractAddress,
    }

    #[storage]
    struct Storage {
        owner: ContractAddress,
    }

    #[constructor]
    fn constructor(ref self: ContractState, init_owner: ContractAddress) {
        self.owner.write(init_owner);
    }

    #[external(v0)]
    impl OwnableImpl of super::OwnableTrait<ContractState> {
        fn transfer_ownership(ref self: ContractState, new_owner: ContractAddress) {
            self.only_owner();
            let prev_owner = self.owner.read();
            self.owner.write(new_owner);
            self.emit(Event::OwnershipTransferred1(OwnershipTransferred1 {
                prev_owner: prev_owner,
                new_owner: new_owner,
            }));
        }

        fn get_owner(self: @ContractState) -> ContractAddress {
            self.owner.read()
        }
    }

    #[generate_trait]
    impl PrivateMethods of PrivateMethodsTrait {
        fn only_owner(self: @ContractState) {
            let caller = get_caller_address();
            assert(caller == self.owner.read(), 'Caller is not the owner');
        }
    }
}
```

### تجزیه اجزا

در ادامه به توضیح مختصری از اجزای قرارداد می پردازیم. وقتی عمیق تر به قاهره برویم به جزئیات بیشتری خواهیم پرداخت، بنابراین اگر با توسعه قراردادهای هوشمند آشنایی ندارید، فعلاً از این بخش صرف نظر کنید.

1. وابستگی ها و رابط:
   * starknet::ContractAddress: نشان دهنده آدرس قرارداد Starknet است.
   * OwnableTrait: توابعی را برای انتقال و گرفتن مالکیت مشخص می کند.
2. مناسبت ها:
   * OwnershipTransferred1: نشان دهنده تغییر مالکیت با جزئیات مالک قبلی و جدید است.
3. ذخیره سازی:
   * ذخیره سازی: وضعیت قرارداد را با آدرس مالک فعلی نگه می دارد.
4. سازنده:
   * قرارداد را با مالک اولیه آغاز می کند.
5. توابع خارجی:
   * توابع انتقال مالکیت و بازیابی اطلاعات مالک فعلی.
6. روش های خصوصی:
   * only\_owner: اگر تماس گیرنده مالک فعلی باشد، اعتبارسنجی می شود.

### فرآیند تدوین

برای کامپایل با استفاده از Remix:

1. ایجاد فایل
   * به تب "File Explorer" در Remix بروید.
   * یک فایل جدید با نام Ownable.cairo ایجاد کنید و کد قبلی را وارد کنید.
2. تلفیقی
   * Choose the `Ownable.cairo` file.
   * در تب "Starknet"، "Compile Ownable.cairo" را انتخاب کنید.
   * پس از کامپایل، یک پوشه "مصنوعات" ظاهر می شود که حاوی قرارداد کامپایل شده در دو قالب مجزا است: Sierra (فایل JSON) و CASM. برای استقرار Starknet، Remix از فایل Sierra استفاده خواهد کرد. در حال حاضر نگران این روند نباشید. در فصل بعد به تفصیل به آن خواهیم پرداخت. در حال حاضر، Remix کامپایل و استقرار را برای ما انجام خواهد داد.

![Artifacts folder after compilation](img/ch01-remix-file.png)

پوشه Artifacts پس از کامپایل

### استقرار در شبکه توسعه

برای راه اندازی قرارداد هوشمند خود، باید مالک اولیه تعریف شود. تابع سازنده به این اطلاعات نیاز دارد.

در اینجا یک راهنمای گام به گام برای استقرار قرارداد هوشمند خود در شبکه توسعه آورده شده است:

1. شبکه مناسب را انتخاب کنید
   * به تب انتخاب محیط بروید.
   * "Remote Devnet" را برای استقرار قرارداد افتتاحیه خود در یک شبکه توسعه انتخاب کنید.
2. یک حساب Devnet را انتخاب کنید
   * در بخش «انتخاب حساب Devnet»، فهرستی از حساب‌های اختصاصی برای devnet انتخاب شده ارائه می‌شود.
   * هر حسابی را انتخاب کنید و آدرس آن را کپی کنید.
3. شروع استقرار
   * به تب "Starknet" بروید.
   * آدرس کپی شده را در متغیر init\_owner وارد کنید.
   * روی "Deploy ownable.cairo" کلیک کنید.

پس از استقرار، ترمینال Remix گزارش های مختلفی را ارسال می کند. این گزارش ها جزئیات مهمی را ارائه می دهند، از جمله:

* transaction\_hash: هش منحصر به فرد تراکنش. از این هش می توان برای ردیابی وضعیت تراکنش استفاده کرد.
* contract\_address: آدرس قرارداد مستقر شده. از این آدرس برای تعامل با قرارداد خود استفاده کنید.
* calldata: حاوی آدرس init\_owner است که به سازنده داده شده است.

```bash
{
  "transaction_hash": "0x275e6d2caf9bc98b47ba09fa9034668c6697160a74de89c4655e2a70be84247",
  "contract_address": "0x5eb239955ad4c4333b8ab83406a3cf5970554b60a0d8e78a531df18c59a0db9",
    ...
  "calldata": [
    "0x4d9c8282b5633eeb1aab56393690d76f71e32f1b7be1bea03eb03e059245a28"
  ],
    ...
}
```

با پیروی از فرآیند فوق، قرارداد هوشمند خود را با موفقیت در شبکه توسعه مستقر می کنید.

### تعامل با قرارداد

با قراردادی که اکنون در شبکه توسعه فعال است، تعامل امکان پذیر می شود. در اینجا راهنمایی برای تعامل موثر با قرارداد خود در Starknet آمده است:

1. شروع تعامل
   * به تب "Starknet" بروید.
   * گزینه «تعامل» را انتخاب کنید.
2. فراخوانی تابع get\_owner
   * تابع get\_owner را انتخاب کنید. از آنجایی که این تابع به آرگومان نیاز ندارد، فیلد calldata خالی می ماند. (این تابع خواندن است، از این رو فراخوانی آن به عنوان "تماس" نامیده می شود.)
   * دکمه "get\_owner" را فشار دهید. ترمینال شما نتیجه را نمایش می‌دهد و نشانی مالک ارائه‌شده در حین استقرار قرارداد را به‌عنوان داده فراخوانی برای سازنده نشان می‌دهد:

```json
{
  "response": {
    "result": [
      "0x4d9c8282b5633eeb1aab56393690d76f71e32f1b7be1bea03eb03e059245a28"
    ]
  },
  "contract": "ownable.cairo",
  "function": "get_owner"
}
```

این تماس در حال حاضر گاز مصرف نمی کند زیرا عملکرد وضعیت قرارداد را تغییر نمی دهد.

3. فراخوانی تابع transfer\_ownership

* اکنون برای تابع transfer\_ownership که به آدرس مالک جدید به عنوان ورودی نیاز دارد.
* این آدرس را در قسمت calldata وارد کنید. (برای این کار، از هر آدرسی از «انتخاب حساب Devnet» فهرست شده در برگه Environment استفاده کنید.)
* روی دکمه "transfer\_ownership" کلیک کنید. سپس ترمینال هش تراکنش را نشان می دهد که نشان دهنده تغییر وضعیت قرارداد است. از آنجایی که ما وضعیت قرارداد را تغییر می‌دهیم، این اشتباه تایپی از تعامل «فراخوانی» نامیده می‌شود و باید توسط حسابی که تابع را فراخوانی می‌کند امضا شود.

برای این تراکنش‌ها، گزارش‌های پایانه یک متغیر «وضعیت» را نشان می‌دهند که سرنوشت تراکنش را نشان می‌دهد. اگر وضعیت "ACCEPTED\_ON\_L2" خوانده شود، ترتیب‌دهنده تراکنش را پذیرفته است، در حال تعلیق درج بلوک. با این حال، وضعیت "رد شده" به معنای عدم تایید ترتیب‌دهنده است و تراکنش در بلوک آینده نمایش داده نخواهد شد. اغلب اوقات، این معامله مورد پذیرش قرار می گیرد و منجر به اصلاح وضعیت قرارداد می شود. با فراخوانی مجدد تابع get\_owner این را دریافت می کنیم:

```json
{
  "response": {
    "result": [
      "0x20884fd341e11a00b9d31600c332f126f5c3f9ffd2aa93cb43dee9f90176d4f"
    ]
  },
  "contract": "ownable.cairo",
  "function": "get_owner"
}
```

شما اکنون ماهرانه قرارداد هوشمند Starknet خود را کامپایل کرده، مستقر کرده و با آن تعامل کرده اید. آفرین!

### استقرار در Starknet Testnet

پس از آزمایش قرارداد هوشمند خود در یک شبکه توسعه، وقت آن است که آن را در Starknet Testnet مستقر کنید. Starknet Testnet یک پلتفرم عمومی برای همه است که برای آزمایش قراردادهای هوشمند و همکاری با توسعه دهندگان همکار ایده آل است.

### ابتدا باید یک حساب Starknet ایجاد کنید.

راه اندازی کیف پول هوشمند

قبل از استقرار قرارداد هوشمند خود در Starknet، باید هزینه تراکنش را مدیریت کنید. در حالی که استقرار در Starknet Goerli Testnet رایگان است، یک حساب کیف پول هوشمند ضروری است. شما می توانید یک کیف پول هوشمند با استفاده از یکی از موارد زیر راه اندازی کنید:

* [Braavos](https://braavos.app/)
* [Argent](https://www.argent.xyz/argent-x/)

هر دو کیف پول Starknet قابل اعتماد هستند که به لطف امکاناتی که Cairo VM به ارمغان می‌آورد، مانند Account Abstraction (برای اطلاعات بیشتر در این مورد به خواندن کتاب ادامه دهید) ویژگی‌های امنیتی و دسترسی پیشرفته‌تری را ارائه می‌دهند.

1. افزونه chrome/brave توصیه شده را برای کیف پول انتخابی خود نصب کنید.
2. دستورالعمل های ارائه دهنده کیف پول خود را برای استقرار حساب خود دنبال کنید.
3. از شیر آب Starknet برای تامین مالی حساب خود استفاده کنید.
4. اکانت را در شبکه مستقر کنید. این معمولاً حدود 10 ثانیه طول می کشد.

پس از راه اندازی، آماده استقرار قراردادهای هوشمند خود در Starknet Testnet هستید.

### استقرار و تعامل

1. مراحل استقرار قبلی را دنبال کنید.
2. در برگه «انتخاب محیط»، «انتخاب کیف پول» را انتخاب کنید.
3. حساب Starknet خود را انتخاب کنید و به استقرار و تعامل با قرارداد خود ادامه دهید.

می‌توانید هش‌ها و آدرس‌های تراکنش را با استفاده از هر کاوشگر بلوک Starknet مانند:

* [Starkscan](https://testnet.starkscan.co/)
* [Voyager](https://goerli.voyager.online/)

این ابزارها نمایشی بصری از تراکنش ها و تغییرات وضعیت قرارداد را ارائه می دهند. قابل توجه است که وقتی مالکیت قرارداد را با استفاده از تابع transfer\_ownership تغییر می‌دهید، رویداد منتشر شده توسط قرارداد در کاوشگر بلوک ظاهر می‌شود. این یک روش موثر برای پیگیری رویدادهای قرارداد است.

### مراحل بعدی شما

جهت خود را از گزینه های زیر تعیین کنید:

1. دانش Starknet خود را عمیق تر کنید: برای درک گسترده از عملکرد درونی Starknet و موارد استفاده بالقوه، به فصل 3 کتاب Starknet بپردازید. این فصل جزئیات معماری Starknet را شرح می دهد. سپس از آنجا به جلو بروید.
2. غواصی در قاهره: اگر بیشتر با برنامه نویسی هماهنگ هستید و می خواهید قراردادهای Starknet را ایجاد کنید، قاهره ضروری است. این زبان قرارداد اصلی Starknet است. با فصل های 1-6 کتاب قاهره شروع کنید، از اصول اولیه شروع تا جنبه های پیشرفته تر مانند Enums و تطبیق الگو. با رفتن به فصل قراردادهای هوشمند Starknet، اطمینان حاصل کنید که درک کاملی دارید.
